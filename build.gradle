allprojects {
    group = 'org.matrixstudio.ocean'
    version = '0.0.1-SNAPSHOT'
}

buildscript {
    ext {
        kotlinVersion = '1.2.71'
        springBootVersion = '2.0.6.RELEASE'
    }
    repositories {
        maven {
            url "http://maven.aliyun.com/nexus/content/groups/public"
        }
        mavenLocal()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-noarg:${kotlinVersion}")
    }
}

// 所有子项目通用配置
subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'kotlin-spring'
    apply plugin: 'kotlin-jpa'
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    ext {
        springSecurityOAuth2AutoConfigurationVersion = "2.0.4.RELEASE"
        lombokVersion = "1.18.2"
        mysqlConnectorVersion = "6.0.6"
        guavaVersion = "26.0-jre"
        springfoxSwagger2Version = "2.9.2"
    }

    repositories {
        maven {
            url "http://maven.aliyun.com/nexus/content/groups/public"
        }
        mavenLocal()
        jcenter()
        mavenCentral()
    }

    dependencies {
        // Kotlin support
        compile('com.fasterxml.jackson.module:jackson-module-kotlin')
        compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
        compile("org.jetbrains.kotlin:kotlin-reflect")

        compile("com.google.guava:guava:${guavaVersion}")
        compileOnly("org.projectlombok:lombok:${lombokVersion}")
        compileOnly("org.springframework.boot:spring-boot-configuration-processor")
    }

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }

    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }
    
    clean.doLast {
        file("${projectDir}/out").deleteDir()
        file("${rootDir}/out").deleteDir()
        file("${rootDir}/build").deleteDir()
    }

    compileJava.dependsOn(processResources)

    // To tell IDEA download source and javadoc
    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    tasks.withType(Javadoc) {
        options.addStringOption("Xdoclint:none", "-quiet")
        options.addStringOption("encoding", "UTF-8")
        options.addStringOption("charSet", "UTF-8")
    }
}